% This file is METAFONT source for hand-written Czech font "slabikar". 
% Author:  Petr Olsak (C) 1997   [Petr Ol\v{s}\'ak]

% Version: Dec. 1997

% You can use this font as free software, i.e. without warranty. If
% you do some changes in this file, you MUST rename this one. This
% font is possible to include into large TeX distributions, if all
% unchanged files are included from: 
% ftp://math.feld.cvut.cz/pub/olsak/slabikar/.


%% Initialising:

mode_setup;
thin#:=.4pt#; u#:=1pt#;
kk#:=2u#;
define_pixels(thin, u);


%% Encoding (accents by ISO-8859-2):

aacute = 225;  Aacute = 193;
bnarrow = 18;
ccaron = 232;  Ccaron = 200;
dcaron = 239;  Dcaron = 207;
eacute = 233;  ecaron = 236;  Eacute = 201;  Ecaron = 204;
iacute = 237;  Iacute = 205;
oacute = 243;  onarrow = 14;  oacutenarrow = 15; Oacute = 211;
ncaron = 242;  Ncaron = 210;
rcaron = 248;  Rcaron = 216;
scaron = 185;  Scaron = 169;
sleft = 10;  scaronleft = 11;  sdepth = 12;  scarondepth = 13;
tcaron = 187;  Tcaron = 171;
uacute = 250;  uring  = 249;  Uacute = 218;  Uring = 217;
vnarrow = 16;  wnarrow =17;
yacute = 253;  Yacute = 221;
zcaron = 190;  Zcaron = 174;

minus = 21;  dash = 22;  times = 23;
clqq = 254;  crqq = 255;
leftquota = 96;  rightquota = 39;
slash = 47;  backslash = 92;
percent = 37;  promile = 141;
inch = 34;

%% Macros and variables:

pair sklon[];
sklon1:=-(1.5,6); sklon2:=(5,6);
dotkoef=1.7;

def dotah =
  draw ((0,1){sklon1}..(1,0){right}..{sklon2}(8,6))
enddef;

def carka =
  draw ((0,9)--(1.5,13))
enddef;

def capcarka =
  draw ((0,15)--(1.5,18))
enddef;

def hacek =
  draw ((0,12)..{sklon1}(-.3,10)..{right}(.8,9)..tension2..{sklon2}(5,12))
enddef;

def krouzek =
   draw ((0,9){right}..(0,11){left}..cycle)
enddef;

def smycka =
   draw ((0,7){sklon1}..{sklon1}(-2,0)..{sklon1}(-3.1,-4)..
       {left}(-5.3,-7)..{-sklon1}(-5.5,-5)..
       (-2,0)..{sklon2}(5.5,6))
enddef;

def kcara =
   draw (9.5,14)--(5,0); draw (0,0)--(0,7)
enddef;

currenttransform:=currenttransform scaled u;
pickup pencircle scaled thin;
let clearpen=\;

%screenstrokes;

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%% The letters:
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

beginchar("a", 9.4u#, 7u#, 0);
  z0=(0,6); z1=(3,7); z2=(-1,0); z3=(1.4,1);
  z1p=(1.5,6.9);
  draw z1{left}..z1p..z0{-sklon2}..tension1.5..
       z2{right}..tension1.2..{-sklon1}z1{sklon1}..z3{sklon1};
  dotah shifted (x3,0);
endchar;

beginchar(aacute, 9.4u#, 13u#, 0);  %% \'a
  z0=(0,6); z1=(3,7); z2=(-1,0); z3=(1.4,1);
  z1p=(1.5,6.9);
  draw z1{left}..z1p..z0{-sklon2}..tension1.5..
       z2{right}..tension1.5..{-sklon1}z1{sklon1}..z3{sklon1};
  dotah shifted (x3,0);
  carka shifted (3,0);
endchar;

beginchar("A", 11.4u#, 14u#, 0);
  z0=(0,6); z1=(7,14); z2=(1,0); z3=(3.4,1);
  z1p=(6,13.9);
  draw z1{left}..z1p..z0{sklon1}..
       z2{right}..tension2.5..{-sklon1}z1{sklon1}..z3{sklon1};
  dotah shifted (x3,0);
endchar;

beginchar(Aacute, 11.4u#, 18u#, 0);
  z0=(0,6); z1=(7,14); z2=(1,0); z3=(3.4,1);
  z1p=(6,13.9);
  draw z1{left}..z1p..z0{sklon1}..
       z2{right}..tension2.5..{-sklon1}z1{sklon1}..z3{sklon1};
  dotah shifted (x3,0);
  capcarka shifted (6,0);
endchar;

beginchar("b", 9u#, 14u#, 0);
  z0=(0,6); z1=(4,14); z2=(1.5,10); z3=(-1,1);
  z4=(0,0); z5=(3,7); z6=(9,6);
  z1p=(4,12); z5p=(2.5,6);
  draw z0{sklon2}..z1p..tension1.5..z1{left}..z2{sklon1}..
       z3{sklon1}..z4{right}..tension3..z5{left}..z5p{down}..
       tension2..{sklon2}z6;
endchar;

beginchar(bnarrow, 7u#, 14u#, 0);
  z0=(0,6); z1=(4,14); z2=(1.5,10); z3=(-1,1);
  z4=(0,0); z5=(3,7); z6=(7,6);
  z1p=(4,12); z5p=(2.5,6);
  draw z0{sklon2}..z1p..tension1.5..z1{left}..z2{sklon1}..
       z3{sklon1}..z4{right}..tension3..z5{left}..z5p{down}..
       tension2..{sklon2}z6;
endchar;

beginchar("B", 9u#, 14u#, 0);
  z0=(0,6);  z1=(8,14);  z2=(1,0);  z2p=(3,3);
  z3=(0,4);  z4=(6,8);  z4l=(x4-1,y4);
  z5=(8.5,4);  z6=(5,0);  z7=(3.5,2);
  draw z1{left}..z2p{sklon1}..z2{left}..z0{-sklon1}..
      z1{right}..{dir190}z4..z4l{up}..z4{dir-20}..z5{sklon1}..z6{left}..z7;
endchar;

beginchar("c", 8u#, 7u#, 0);
  z0=(0,6); z1=(1.5,7); z2=(2.5,6); z3=(0,0); z4=(8,6);
  draw z2..z1{left}..z0{-sklon2}..z3{right}..{sklon2}z4;
endchar;

beginchar(ccaron, 8u#, 12u#, 0);
  z0=(0,6); z1=(1.5,7); z2=(2.5,6); z3=(0,0); z4=(8,6);
  draw z2..z1{left}..z0{-sklon2}..z3{right}..{sklon2}z4;
  hacek shifted (2,0);
endchar;

beginchar("C", 9.5u#, 14u#, 0);
  z0=(0,6);  z1=(5,12.5);  z2=(x1+.3,11);  z3=(x1+.1,14);
  z4=(1.5,0);  z5=(x4+8,6);
  draw z1{sklon1}..z2{right}..tension1.5..z3{left}..tension2..
       z0{sklon1}..z4{right}..tension1.5..{sklon2}z5;
endchar;

beginchar(Ccaron, 9.5u#, 18u#, 0);
  z0=(0,6);  z1=(5,12.5);  z2=(x1+.3,11);  z3=(x1+.1,14);
  z4=(1.5,0);  z5=(x4+8,6);
  draw z1{sklon1}..z2{right}..tension1.5..z3{left}..tension2..
       z0{sklon1}..z4{right}..tension1.5..{sklon2}z5;
  hacek shifted (5,6);
endchar;

beginchar("d", 9.4u#, 14u#, 0);
  z0=(0,6); z1=(3,7); z2=(-1,0); z3=(1.4,1);
  z1p=(1.5,6.9);
  z1n-z3 = whatever * (z1-z3); y1n=14;
  draw z1{left}..z1p..z0{-sklon2}..tension1.5..
       z2{right}..tension1.5..{-sklon1}z1--z1n{sklon1}..z3{sklon1};
  dotah shifted (x3,0);
endchar;

beginchar(dcaron, 9.4u#, 14u#, 0); %% \v d
  z0=(0,6); z1=(3,7); z2=(-1,0); z3=(1.4,1);
  z1p=(1.5,6.9);
  z1n-z3 = whatever * (z1-z3); y1n=14;
  draw z1{left}..z1p..z0{-sklon2}..tension1.5..
       z2{right}..tension1.5..{-sklon1}z1--z1n{sklon1}..z3{sklon1};
  dotah shifted (x3,0);
  hacek shifted (6,2);
endchar;

beginchar("D", 8u#, 14u#, 0);
  z1=(9,14);  z2=(3,3);  z3=(1,0);  z4=(.5,0); z4n=(x4,1);
  z5=(4,0); z6=(7,4);  z7=(6,14);  z8=(3.5,13);  z9=(3.5,9);
  z10=(5.5,12);
  draw z1{-sklon2}..z2{sklon1}..z3..z4{left}..z4n{right}..
       z5{right}..z6{-sklon1}..z7{left}..
       z8{-sklon2}..z9{right}..{-sklon1}z10;
endchar;

beginchar(Dcaron, 8u#, 18u#, 0);
  z1=(9,14);  z2=(3,3);  z3=(1,0);  z4=(.5,0); z4n=(x4,1);
  z5=(4,0); z6=(7,4);  z7=(6,14);  z8=(3.5,13);  z9=(3.5,9);
  z10=(5.5,12);
  draw z1{-sklon2}..z2{sklon1}..z3..z4{left}..z4n{right}..
       z5{right}..z6{-sklon1}..z7{left}..
       z8{-sklon2}..z9{right}..{-sklon1}z10;
  hacek shifted (7,6);
endchar;

beginchar("e", 5u#, 7u#, 0);
  z0=(0,6); z1=(-1,7); z2=(-2,5); z3=(-3,1);
  draw z0{sklon2}..z1{left}..z2{sklon1}..{sklon1}z3;
  dotah shifted (x3,0);
endchar;

beginchar(eacute, 5u#, 13u#, 0);  %% \'e
  z0=(0,6); z1=(-1,7); z2=(-2,5); z3=(-3,1);
  draw z0{sklon2}..z1{left}..z2{sklon1}..{sklon1}z3;
  dotah shifted (x3,0);
  carka;
endchar;

beginchar(ecaron, 5u#, 12u#, 0);  %% \v e
  z0=(0,6); z1=(-1,7); z2=(-2,5); z3=(-3,1);
  draw z0{sklon2}..z1{left}..z2{sklon1}..{sklon1}z3;
  dotah shifted (x3,0);
  hacek shifted (-.5,0);
endchar;

beginchar("E", 9.5u#, 14u#, 0);
  z0=(0,6);  z1=(4,12.5);  z2=(x1+.3,11);  z3=(x1+.1,14);
  z6=(2,8.5);  z6n=(x6+1,y6);
  z4=(1.5,0);  z5=(x4+8,6);
  draw z1{sklon1}..z2{right}..tension1.5..z3{left}..tension2..
       z6..z6n{up}..z6..
       z0..tension1.5and1..z4{right}..tension1.5..{sklon2}z5;
endchar;

beginchar(Eacute, 9.5u#, 18u#, 0);
  z0=(0,6);  z1=(4,12.5);  z2=(x1+.3,11);  z3=(x1+.1,14);
  z6=(2,8.5);  z6n=(x6+1,y6);
  z4=(1.5,0);  z5=(x4+8,6);
  draw z1{sklon1}..z2{right}..tension1.5..z3{left}..tension2..
       z6..z6n{up}..z6..
       z0..tension1.5and1..z4{right}..tension1.5..{sklon2}z5;
  capcarka shifted (5,0);
endchar;

beginchar(Ecaron, 9.5u#, 18u#, 0);
  z0=(0,6);  z1=(4,12.5);  z2=(x1+.3,11);  z3=(x1+.1,14);
  z6=(2,8.5);  z6n=(x6+1,y6);
  z4=(1.5,0);  z5=(x4+8,6);
  draw z1{sklon1}..z2{right}..tension1.5..z3{left}..tension2..
       z6..z6n{up}..z6..
       z0..tension1.5and1..z4{right}..tension1.5..{sklon2}z5;
  hacek shifted (4.5,6);
endchar;

beginchar("f", 7u#, 14u#, 7u#);
  z0=(0,6); z1=(4,14); z2=(1.5,10); z3=(-1.2,0);
  z1p=(4,12);
  z3r=(-.8,0); z3l=(-1.6,.3);
  z4=(-3,-6);  z5=(-2.3,-7);  z6=(7,6);
  draw z0{sklon2}..z1p..tension1.5..z1{left}..z2{sklon1}..z3{sklon1}
       ..z4{sklon1}..z5{right}..tension3..
       {(-2,1)}z3r..z3l{down}..z3r{right}..{sklon2}z6;
endchar;

beginchar("F", 5u#, 14u#, 0);  charic := 7u#;
   z1=(4,12.5);  z2=(3,10);  z3=(2.3,12.5);  z4=(4,14);
   z5=(7,13.5);  z6=(12,14);
   z7=(4,4);  z8=(1,0);  z9=(0,3);
   z10=(2,7);  z11=(6,9);  z12=(6.5,7);
   draw z1{sklon1}..z2..z3{-sklon1}..z4{right}..z5..{right}z6;
   draw z5{-sklon2}..z7{sklon1}..z8{left}..{-sklon1}z9;
   draw z10..z11{right}..{sklon1}z12;
endchar;

beginchar("g", 8.5u#, 7u#, 7u#);
  z0=(0,6); z1=(3,7); z2=(-1,0); z3=(1.4,1);
  z1p=(1.5,6.9);
  draw z1{left}..z1p..z0{-sklon2}..tension1.5..
       z2{right}..tension1.5..{-sklon1}z1;
  smycka shifted (x1,0);
endchar;

beginchar("G", 10.5u#, 14u#, 7u#);
  z0=(0,6);  z1=(5,12.5);  z2=(x1+.3,11);  z3=(x1+.1,14);
  z4=(1,0);  z5=(x4+4,7);
  draw z1{sklon1}..z2{right}..tension1.5..z3{left}..tension2..
       z0{sklon1}..z4{right}..tension1.5..{-sklon1}z5;
  smycka shifted (x5,0);
endchar;

beginchar("h", 9.85u#, 14u#, 0);
  z0=(0,6); z1=(4,14); z2=(1.5,10); z3=(-1.2,0);
  z1p=(4,12);
  z4=(0.15,5);
  z5n=(x4+2.5,7); z5=(x4+3,6); z6=(x4+1.7,1);
  draw z0{sklon2}..z1p..tension1.5..z1{left}..z2{sklon1}..z3{sklon1};
  draw z4{-sklon1}..z5n{right}..z5{sklon1}..{sklon1}z6;
  dotah shifted (x6,0);
endchar;

beginchar("H", 13u#, 14u#, 0);
  z1=(1.5,11);  z2=(4.5,14);  z3=(5,12);  z4=(2,1);
  z5=(1,0);  z6=(0.5,3);  z7=(6,8);
  z8=(9.5,11.5);  z9=(9,14);  z10=(8,13);  z11=(5,1);
  draw z1{sklon2}..z2{right}..z3{sklon1}..{sklon1}z4..
       z5{left}..z6{-sklon1}..tension1.5..z7..z8{-sklon1}..
       z9{left}..z10{sklon1}..{sklon1}z11;
  dotah shifted (x11,0);
endchar;

beginchar("i", 7u#, 11u#, 0);
  z0=(0,6); z1=(.5,7); z2=(-1,1); z3-z1=whatever*sklon1; y3=11;
  pickup pencircle scaled (dotkoef*thin);
  drawdot z3;
  pickup pencircle scaled thin;
  draw z0{sklon2}..{-sklon1}z1{sklon1}..{sklon1}z2;
  dotah shifted (x2,0);
endchar;

beginchar(iacute, 7u#, 13u#, 0);  %% \'i
  z0=(0,6); z1=(.5,7); z2=(-1,1); z3-z1=whatever*sklon1; y3=9;
  draw z0{sklon2}..{-sklon1}z1{sklon1}..{sklon1}z2;
  dotah shifted (x2,0);
  carka shifted (x3,0);
endchar;

beginchar("I", 5u#, 14u#, 0);
  z1=(2.5,11);  z2=(5.5,14);  z3=(6,12);  z4=(3.2,2);
  z5=(1,0);  z6=(-.5,3);
  draw z1{sklon2}..z2{right}..z3{sklon1}..{sklon1}z4..
       z5{left}..{up}z6;
endchar;

beginchar(Iacute, 5u#, 18u#, 0);
  z1=(2.5,11);  z2=(5.5,14);  z3=(6,12);  z4=(3.2,2);
  z5=(1,0);  z6=(-.5,3);
  draw z1{sklon2}..z2{right}..z3{sklon1}..{sklon1}z4..
       z5{left}..{up}z6;
  capcarka shifted (6,0);
endchar;

beginchar("j", 6u#, 11u#, 7u#);
  z0=(0,6); z1=(.5,7);
  z3-z1=whatever*sklon1; y3=11;
  pickup pencircle scaled (dotkoef*thin);
  drawdot z3;
  pickup pencircle scaled thin;
  draw z0{sklon2}..{-sklon1}z1;
  smycka shifted (x1,0);
endchar;

beginchar("J", 10.1u#, 14u#, 7u#);
  z1=(2.5,11);  z2=(5.5,14);  z3=(6,12);  z4=(4.6,7);
  draw z1{sklon2}..z2{right}..z3{sklon1}..{sklon1}z4;
  smycka shifted (x4,0);
endchar;

beginchar("k", 10u#, 14u#, 0);
  z0=(0,6); z1=(4,14); z2=(1.5,10); z3=(-1.2,0);
  z1p=(4,12);
  (z4-z3) = whatever*(z2-z3);  y4=5;
  z5=(2.3,7);  z6=(2,4.5);  z7=(1,y6);  z8=(4,0);  z9=(10,6);
  draw z0{sklon2}..z1p..tension1.5..z1{left}..z2{sklon1}..z3{sklon1};
  draw z4{-sklon1}..z5{right}..z6..z7{up}..z6..
  z8{right}..tension3..{sklon2}z9;
endchar;

beginchar("K", 15u#, 14u#, 0);
  z1=(2.5,11);  z2=(5.5,14);  z3=(6,12);  z4=(3.2,2);
  z5=(1,0);  z6=(-.5,3);
  z7=(12,11);  z8=(10,14);  z9=(5,8);  z9p=(x9+1,y9);
  z10=(9,0);  z11=(15,6);
  draw z1{sklon2}..z2{right}..z3{sklon1}..{sklon1}z4..
       z5{left}..{up}z6;
  draw z7{dir110}..z8{left}..tension2..
       z9p{dir215}..z9{up}..z9p{dir-35}..
       tension1.5..z10{right}..tension3..{sklon2}z11;
endchar;

beginchar("l", 7u#, 14u#, 0);
  z0=(0,6); z1=(4,14); z2=(1.5,10); z3=(-1,1);
  z1p=(4,12);
  draw z0{sklon2}..z1p..tension1.5..z1{left}..z2{sklon1}..z3{sklon1};
  dotah shifted (x3,0);
endchar;

beginchar("L", 12u#, 14u#, 0);
  z1=(4,14);  z2=(6,9);  z3=(9,14);  z4=(7,12);
  z5=(2,1);  z5d=(.5,0);  z5p=(-.5,.8);  z6=(6,0);  z7=(12,6);
  draw z1..z2{right}..tension1.7..z3{left}..z4{sklon1}..
       z5..z5d{left}..z5p{up}..z5..z6{right}..tension2..{sklon2}z7;
endchar;

beginchar("m", 15.7u#, 7u#, 0);
  z0=(0,6); z1=(3,5); z1d=(1.5,0); z2=(6,5); z2d=(4.5,0);
  z3=(9,5); z4=(7.7,1);
  z1n=(2.5,7); z2n=(5.5,7); z3n=(8.5,7);
  draw z0{sklon2}..z1n{right}..z1{sklon1}..{sklon1}z1d;
  draw z1{-sklon1}..z2n{right}..z2{sklon1}..{sklon1}z2d;
  draw z2{-sklon1}..z3n{right}..z3{sklon1}..{sklon1}z4;
  dotah shifted (x4,0);
endchar;

beginchar("M", 15u#, 14u#, 0);
  z1=(0.5,11);  z2=(3.5,14);  z3=(4,12);  z4=(0.8,0);
                z5=(6.5,14);  z6=(7,12);  z7=(3.8,0);
                z8=(9.5,14);  z9=(10,12);  z10=(7,1);
  draw z1{sklon2}..z2{right}..z3{sklon1}..{sklon1}z4;
  draw z3{-sklon1}..tension1.5..z5{right}..z6{sklon1}..{sklon1}z7;
  draw z6{-sklon1}..tension1.5..z8{right}..z9{sklon1}..{sklon1}z10;
  dotah shifted (x10,0);
endchar;

beginchar("n", 12.7u#, 7u#, 0);
  z0=(0,6); z1=(3,5); z1d=(1.5,0); z2=(6,5); z3=(4.7,1);
  z1n=(2.5,7); z2n=(5.5,7);
  draw z0{sklon2}..z1n{right}..z1{sklon1}..{sklon1}z1d;
  draw z1{-sklon1}..z2n{right}..z2{sklon1}..{sklon1}z3;
  dotah shifted (x3,0);
endchar;

beginchar(ncaron, 12.7u#, 12u#, 0);
  z0=(0,6); z1=(3,5); z1d=(1.5,0); z2=(6,5); z3=(4.7,1);
  z1n=(2.5,7); z2n=(5.5,7);
  draw z0{sklon2}..z1n{right}..z1{sklon1}..{sklon1}z1d;
  draw z1{-sklon1}..z2n{right}..z2{sklon1}..{sklon1}z3;
  dotah shifted (x3,0);
  hacek shifted (4,0);
endchar;

beginchar("N", 12u#, 14u#, 0);
  z1=(0.5,11);  z2=(3.5,14);  z3=(4,12);  z4=(0.8,0);
                z5=(6.5,14);  z6=(7,12);  z7=(4,1);
  draw z1{sklon2}..z2{right}..z3{sklon1}..{sklon1}z4;
  draw z3{-sklon1}..tension1.5..z5{right}..z6{sklon1}..{sklon1}z7;
  dotah shifted (x7,0);
endchar;

beginchar(Ncaron, 12u#, 18u#, 0);
  z1=(0.5,11);  z2=(3.5,14);  z3=(4,12);  z4=(0.8,0);
                z5=(6.5,14);  z6=(7,12);  z7=(4,1);
  draw z1{sklon2}..z2{right}..z3{sklon1}..{sklon1}z4;
  draw z3{-sklon1}..tension1.5..z5{right}..z6{sklon1}..{sklon1}z7;
  dotah shifted (x7,0);
  hacek shifted (5,6);
endchar;

beginchar("o", 8u#, 7u#, 0);
  z0=(0,6); z1=(3,7); z2=(-1,0); z3=(8,6);
  z1p=(1.5,6.9); z2p=(2,6);
  draw z1{left}..z1p..z0{-sklon2}..tension1.5..
       z2{right}..tension1.5..{-sklon1}z1{left}..z2p{down}..{sklon2}z3;
endchar;

beginchar(onarrow, 6u#, 7u#, 0);
  z0=(0,6); z1=(3,7); z2=(-1,0); z3=(6,6);
  z1p=(1.5,6.9); z2p=(2,6);
  draw z1{left}..z1p..z0{-sklon2}..tension1.5..
       z2{right}..tension1.5..{-sklon1}z1{left}..z2p{down}..{sklon2}z3;
endchar;

beginchar(oacute, 8u#, 13u#, 0);  %% \'o
  z0=(0,6); z1=(3,7); z2=(-1,0); z3=(8,6);
  z1p=(1.5,6.9); z2p=(2,6);
  draw z1{left}..z1p..z0{-sklon2}..tension1.5..
       z2{right}..tension1.5..{-sklon1}z1{left}..z2p{down}..{sklon2}z3;
  carka shifted (3,0);
endchar;

beginchar(oacutenarrow, 6u#, 13u#, 0);  %% \'o
  z0=(0,6); z1=(3,7); z2=(-1,0); z3=(6,6);
  z1p=(1.5,6.9); z2p=(2,6);
  draw z1{left}..z1p..z0{-sklon2}..tension1.5..
       z2{right}..tension1.5..{-sklon1}z1{left}..z2p{down}..{sklon2}z3;
  carka shifted (3,0);
endchar;

beginchar("O", 5u#, 14u#, 0);  charic := 2u#;
  z0=(0,6); z1=(6,14); z2=(1,0); z3=(10,14);
  z1p=(4.5,13.9); z2p=(5.5,13);  z1d=(6.5,13);
  draw z1{left}..z1p..tension1.5and1..z0{sklon1}..tension1.5..
       z2{right}..tension2..z1d..z1{left}..z2p{down}..{sklon2}z3;
endchar;

beginchar(Oacute, 5u#, 18u#, 0);  charic := 2u#;
  z0=(0,6); z1=(6,14); z2=(1,0); z3=(10,14);
  z1p=(4.5,13.9); z2p=(5.5,13);  z1d=(6.5,13);
  draw z1{left}..z1p..tension1.5and1..z0{sklon1}..tension1.5..
       z2{right}..tension2..z1d..z1{left}..z2p{down}..{sklon2}z3;
  capcarka shifted (6,0);
endchar;

beginchar("p", 10.2u#, 8u#, 7u#);
  z0=(0,6);
  z1=(.5,4);  z2n=(3,7); z2=(3.5,6); z3=(2.2,1);
  z5=(-2.5,-7);
  (z1-z4)=whatever*(z1-z5); y4=8;
  draw z0{sklon2}..{-sklon1}z4{sklon1}--z5;
  draw z1{-sklon1}..z2n{right}..z2{sklon1}..{sklon1}z3;
  dotah shifted (x3,0);
endchar;

beginchar("P", 4u#, 14u#, 0);   charic := 5u#;
  z0=(0,6);  z1=(8,14);  z2=(1,0);  z2p=(3,3);
  z3=(0,4);  z4=(5,8);
  draw z1{left}..z2p{sklon1}..z2{left}..z0{-sklon1}..
      z1{right}..{left}z4;
endchar;

beginchar("q", 9u#, 7u#, 7u#);
  z0=(0,6); z1=(3,7); z2=(-1,0);
  z1p=(1.5,6.9);
  z3=(1.1,-.5); z4-z1 = whatever*(z3-z1); y4=-7;
  z5=(9,6);
  draw z1{left}..z1p..z0{-sklon2}..tension1.5..
       z2{right}..tension1.5..{-sklon1}z1{sklon1}..{sklon1}z3..{sklon1}z4;
  draw z3{-sklon1}..tension2..{sklon2}z5;
endchar;

beginchar("Q", 10.9u#, 14u#, 7u#);
  z0=(0,6);  z1=(5,12.5);  z2=(x1+.3,11);  z3=(x1+.1,14);
  z4=(1,0);  z5=(5,7);
  z6=(3,-.5);  z7-z6 = whatever*(z5-z6);  y7=-7;
  z8=(x6+7.9,6);
  draw z1{sklon1}..z2{right}..tension1.5..z3{left}..tension2..
       z0{sklon1}..z4{right}..tension1.5..
       {-sklon1}z5{sklon1}..z6{sklon1}..{sklon1}z7;
  draw z6{-sklon1}..tension2..{sklon2}z8;
endchar;

beginchar("r", 8.4u#, 7u#, 0);
  z0=(0,6); z1=(.5,7); z2=(2,7); z3=(.4,1);
  draw z0{sklon2}..{-sklon1}z1{sklon1}..{-sklon1}z2{sklon1}..{sklon1}z3;
  dotah shifted (x3,0);
endchar;

beginchar(rcaron, 8.4u#, 12u#, 0);
  z0=(0,6); z1=(.5,7); z2=(2,7); z3=(.4,1);
  draw z0{sklon2}..{-sklon1}z1{sklon1}..{-sklon1}z2{sklon1}..{sklon1}z3;
  dotah shifted (x3,0);
  hacek shifted (1.4,0);
endchar;

beginchar("R", 14u#, 14u#, 0);
  z0=(0,6);  z1=(8,14);  z2=(1,0);  z2p=(3,3);
  z3=(0,4);  z4=(6,8);  z4l=(x4-1,y4);
  z10=(8,0);  z11=(14,6);
  draw z1{left}..z2p{sklon1}..z2{left}..z0{-sklon1}..
      z1{right}..{dir215}z4..z4l{up}..z4{dir-35}..
       tension1.5..z10{right}..tension3..{sklon2}z11;
endchar;

beginchar(Rcaron, 14u#, 18u#, 0);
  z0=(0,6);  z1=(8,14);  z2=(1,0);  z2p=(3,3);
  z3=(0,4);  z4=(6,8);  z4l=(x4-1,y4);
  z10=(8,0);  z11=(14,6);
  draw z1{left}..z2p{sklon1}..z2{left}..z0{-sklon1}..
      z1{right}..{dir215}z4..z4l{up}..z4{dir-35}..
       tension1.5..z10{right}..tension3..{sklon2}z11;
  hacek shifted (7,6);
endchar;

beginchar("s", 2u#, 7u#, 0);
  z0=(0,6); z1=(.5,7); z2=(-.5,1); z3=(-2,0); z4=(-3,2); z5=(7,6);
  draw z0{sklon2}..{-sklon1}z1{sklon1}..z2{-sklon2}..z3{left}..{(-1,3)}z4;
endchar;

beginchar(scaron, 2u#, 12u#, 0);  %% \v s
  z0=(0,6); z1=(.5,7); z2=(-.5,1); z3=(-2,0); z4=(-3,2); z5=(7,6);
  draw z0{sklon2}..{-sklon1}z1{sklon1}..z2{-sklon2}..z3{left}..{(-1,3)}z4;
  hacek;
endchar;

beginchar(sleft, 9u#, 7u#, 0);  %% koncove s
  z0=(0,0); z1=(7.5,7); z2=(6.5,1); z3=(5,0); z4=(3.5,3);
  draw z0..{sklon2}z1{-sklon2}..z2{-sklon2}..z3{left}..{(-1,3)}z4;
endchar;

beginchar(sdepth, 2u#, 7u#, 0);
  z0=(0,6); z1=(.5,7); z2=(-.5,1); z3=(-2,0); z4=(-3.5,2.5); z5=(7,6);
  draw z0{sklon2}..{-sklon1}z1{sklon1}..z2{-sklon2}..z3{left}..{(-1,3)}z4;
endchar;

beginchar(scarondepth, 2u#, 12u#, 0);
  z0=(0,6); z1=(.5,7); z2=(-.5,1); z3=(-2,0); z4=(-3.5,2.5); z5=(7,6);
  draw z0{sklon2}..{-sklon1}z1{sklon1}..z2{-sklon2}..z3{left}..{(-1,3)}z4;
  hacek;
endchar;

beginchar(scaronleft, 9u#, 12u#, 0);  %% koncove \v s
  z0=(0,0); z1=(7.5,7); z2=(6.5,1); z3=(5,0); z4=(3.5,3);
  draw z0..{sklon2}z1{-sklon2}..z2{-sklon2}..z3{left}..{(-1,3)}z4;
  hacek shifted (8,0);
endchar;

beginchar("S", 6u#, 14u#, 0);   charic := 3u#;
  z1=(4,14);  z2=(6,9);  z3=(9,14);  z4=(7,12);
  z5=(4.2,2);  z6=(x5-2.2,0); z7=(x6-1.5,3);
  draw z1..z2{right}..tension1.7..z3{left}..z4{sklon1}..{sklon1}z5..
       z6{left}..{up}z7;
endchar;

beginchar(Scaron, 6u#, 18u#, 0);   charic := 3u#;
  z1=(4,14);  z2=(6,9);  z3=(9,14);  z4=(7,12);
  z5=(4.2,2);  z6=(x5-2.2,0); z7=(x6-1.5,3);
  draw z1..z2{right}..tension1.7..z3{left}..z4{sklon1}..{sklon1}z5..
       z6{left}..{up}z7;
  hacek shifted (7,6);
endchar;

beginchar("t", 9u#, 14u#, 0);
  z0=(0,6); z1=(4.5,14); z2=(1,0); z3=(9,6);
  z10=(0,1);
  draw z0{sklon2}..tension2..{-sklon1}z1--z2{-sklon1}..
       z10..z2{right}..z3{sklon2};
endchar;

beginchar(tcaron, 9u#, 14u#, 0);  %% \v t
  z0=(0,6); z1=(4.5,14); z2=(1,0); z3=(9,6);
  z10=(0,1);
  draw z0{sklon2}..tension2..{-sklon1}z1--z2{-sklon1}..
       z10..z2{right}..z3{sklon2};
  hacek shifted (5.5,2);
endchar;

beginchar("T", 5u#, 14u#, 0);   charic := 7u#;
   z1=(4,12.5);  z2=(3,10);  z3=(2.3,12.5);  z4=(4,14);
   z5=(7,13.5);  z6=(12,14);
   z7=(4,4);  z8=(1,0);  z9=(0,3);
   draw z1{sklon1}..z2..z3{-sklon1}..z4{right}..z5..{right}z6;
   draw z5{-sklon2}..z7{sklon1}..z8{left}..{-sklon1}z9;
endchar;

beginchar(Tcaron, 5u#, 18u#, 0);  charic := 7u#;
   z1=(4,12.5);  z2=(3,10);  z3=(2.3,12.5);  z4=(4,14);
   z5=(7,13.5);  z6=(12,14);
   z7=(4,4);  z8=(1,0);  z9=(0,3);
   draw z1{sklon1}..z2..z3{-sklon1}..z4{right}..z5..{right}z6;
   draw z5{-sklon2}..z7{sklon1}..z8{left}..{-sklon1}z9;
   hacek shifted (7,6);
endchar;

beginchar("u", 10u#, 7u#, 0);
  z0=(0,6); z1=(.5,7); z2=(-1,1); z3=(0,0); z4=(3.5,7); z5=(2,1);
  draw z0{sklon2}..{-sklon1}z1{sklon1}..z2{sklon1}..z3{right}..tension2..
       {-sklon1}z4{sklon1}..{sklon1}z5;
  dotah shifted (x5,0);
endchar;

beginchar(uacute, 10u#, 13u#, 0);  %% \'u
  z0=(0,6); z1=(.5,7); z2=(-1,1); z3=(0,0); z4=(3.5,7); z5=(2,1);
  draw z0{sklon2}..{-sklon1}z1{sklon1}..z2{sklon1}..z3{right}..tension2..
       {-sklon1}z4{sklon1}..{sklon1}z5;
  dotah shifted (x5,0);
  carka shifted (2.5,0);
endchar;

beginchar(uring, 10u#, 11u#, 0);  %% \r u
  z0=(0,6); z1=(.5,7); z2=(-1,1); z3=(0,0); z4=(3.5,7); z5=(2,1);
  draw z0{sklon2}..{-sklon1}z1{sklon1}..z2{sklon1}..z3{right}..tension2..
       {-sklon1}z4{sklon1}..{sklon1}z5;
  dotah shifted (x5,0);
  krouzek shifted (2.5,0);
endchar;

beginchar("U", 11.7u#, 14u#, 0);
  z1=(-.5,11);  z2=(2.5,14);  z3=(3,12);  z4=(0,1);
  z5=(0.5,0);  z6=(7,14);  z7=(3.7,1);
  draw z1{sklon2}..z2{right}..z3{sklon1}..{sklon1}z4..
       z5{right}..tension1.7..{-sklon1}z6--z7;
  dotah shifted (x7,0);
endchar;

beginchar(Uacute, 11.7u#, 18u#, 0);
  z1=(-.5,11);  z2=(2.5,14);  z3=(3,12);  z4=(0,1);
  z5=(0.5,0);  z6=(7,14);  z7=(3.7,1);
  draw z1{sklon2}..z2{right}..z3{sklon1}..{sklon1}z4..
       z5{right}..tension1.7..{-sklon1}z6--z7;
  dotah shifted (x7,0);
  capcarka shifted (5.5,0);
endchar;

beginchar(Uring, 11.7u#, 17u#, 0);
  z1=(-.5,11);  z2=(2.5,14);  z3=(3,12);  z4=(0,1);
  z5=(0.5,0);  z6=(7,14);  z7=(3.7,1);
  draw z1{sklon2}..z2{right}..z3{sklon1}..{sklon1}z4..
       z5{right}..tension1.7..{-sklon1}z6--z7;
  dotah shifted (x7,0);
  krouzek shifted (5.5,6);
endchar;

beginchar("v", 11u#, 7u#, 0);
  z0=(0,6); z1=(2.5,6); z11=(2,7);  z2=(1,1);
  z4=(2,0); z5=(5,7); z6=(11,6);
  z5p=(4.5,6);
  draw z0{sklon2}..z11{right}..z1{sklon1}..z2{sklon1}..
       z4{right}..tension3..z5{left}..z5p{down}..
       tension2..{sklon2}z6
endchar;

beginchar(vnarrow, 9u#, 7u#, 0);
  z0=(0,6); z1=(2.5,6); z11=(2,7);  z2=(1,1);
  z4=(2,0); z5=(5,7); z6=(9,6);
  z5p=(4.5,6);
  draw z0{sklon2}..z11{right}..z1{sklon1}..z2{sklon1}..
       z4{right}..tension3..z5{left}..z5p{down}..
       tension2..{sklon2}z6
endchar;

beginchar("V", 5.5u#, 14u#, 0);  charic := 5u#;
  z1=(-.5,11);  z2=(2.5,14);  z3=(3,12);  z4=(0,1);
  z5=(0.5,0);  z9=(6.5,14);  z9d=(x9+.5,y9-1);  z9p=(x9-.5,y9-1);
  z10=(10.5,14);
  draw z1{sklon2}..z2{right}..z3{sklon1}..{sklon1}z4..
       z5{right}..tension1.7..{-sklon1}z9d..
       z9{left}..z9p{down}..{sklon2}z10;
endchar;

beginchar("w", 14u#, 7u#, 0);
  z0=(0,6); z1=(2.5,6); z11=(2,7);
  z2=(1,1); z3=(2,0); z4=(5.5,7);
  z5=(4,1); z6=(5,0); z7=(8,7); z8=(14,6);
  z7p=(7.5,6);
  draw z0{sklon2}..z11{right}..z1{sklon1}..z2{sklon1}..
       z3{right}..tension2..{-sklon1}z4{sklon1}..
       z5{sklon1}..z6{right}..tension3..z7{left}..z7p{down}..
       tension2..{sklon2}z8
endchar;

beginchar(wnarrow, 12u#, 7u#, 0);
  z0=(0,6); z1=(2.5,6); z11=(2,7);
  z2=(1,1); z3=(2,0); z4=(5.5,7);
  z5=(4,1); z6=(5,0); z7=(8,7); z8=(12,6);
  z7p=(7.5,6);
  draw z0{sklon2}..z11{right}..z1{sklon1}..z2{sklon1}..
       z3{right}..tension2..{-sklon1}z4{sklon1}..
       z5{sklon1}..z6{right}..tension3..z7{left}..z7p{down}..
       tension2..{sklon2}z8
endchar;

beginchar("W", 8.3u#, 14u#, 0);  charic := 5u#;
  z1=(-.5,11);  z2=(2.5,14);  z3=(3,12);
  z4=(0,1);    z5=(0.5,0);    z6=(7,14);
  z7=(3.8,1);  z8=(4.3,0);
  z9=(10.3,14);  z9d=(x9+.5,y9-1);  z9p=(x9-.5,y9-1);
  z10=(14.7,14);
  draw z1{sklon2}..z2{right}..z3{sklon1}..{sklon1}z4..
       z5{right}..tension1.7..{-sklon1}z6{sklon1}..
       {sklon1}z7..z8{right}..tension1.7..{-sklon1}z9d..
       z9{left}..z9p{down}..{sklon2}z10;
endchar;

beginchar("x", 5u#, 7u#, 0);
  z0=(0,6); z1=(1,7);
  z2=(-1.5,7);  z3=(-3,1);
  draw z0{sklon2}..z1;
  draw z2{sklon1}..{sklon1}z3;
  dotah shifted (x3,0);
endchar;

beginchar("X", 15.1u#, 14u#, 0);
  z1=(2.5,11);  z2=(5.5,14);  z3=(7,12);  z4=(7.1,1);
  z7=(14,11);  z8=(12,14);  z10=(7,8);  z9=(1,0);  z6=(-.5,3);
  draw z1{sklon2}..z2{right}..z3--z4;
  draw z7{dir110}..z8{left}..tension2..z10..tension1.5..{left}z9..{up}z6;
  dotah shifted (x4,0);
endchar;

beginchar("y", 11u#, 7u#, 7u#);
  z0=(0,6); z1=(2.5,6); z11=(2,7);
  z2=(1,1); z3=(2,0); z4=(5.5,7);
  draw z0{sklon2}..z11{right}..z1{sklon1}..z2{sklon1}..
       z3{right}..tension2..{-sklon1}z4;
  smycka shifted (x4,0);
endchar;

beginchar(yacute, 11u#, 13u#, 7u#);  %% \'y
  z0=(0,6); z1=(2.5,6); z11=(2,7);
  z2=(1,1); z3=(2,0); z4=(5.5,7);
  draw z0{sklon2}..z11{right}..z1{sklon1}..z2{sklon1}..
       z3{right}..tension2..{-sklon1}z4;
  smycka shifted (x4,0);
  carka shifted (4.5,0);
endchar;

beginchar("Y", 10.7u#, 14u#, 7u#);
  z1=(-.5,11);  z2=(2.5,14);  z3=(3,12);  z4=(0,1);
  z5=(0.5,0);  z6=(7,14);  z7=(5.2,7);
  draw z1{sklon2}..z2{right}..z3{sklon1}..{sklon1}z4..
       z5{right}..tension1.7..{-sklon1}z6--z7;
  smycka shifted (x7, 0);
endchar;

beginchar(Yacute, 10.7u#, 18u#, 7u#);
  z1=(-.5,11);  z2=(2.5,14);  z3=(3,12);  z4=(0,1);
  z5=(0.5,0);  z6=(7,14);  z7=(5.2,7);
  draw z1{sklon2}..z2{right}..z3{sklon1}..{sklon1}z4..
       z5{right}..tension1.7..{-sklon1}z6--z7;
  smycka shifted (x7, 0);
  capcarka shifted (5.5,0);
endchar;

beginchar("z", 11u#, 7u#, 0);
  z0=(0,6); z1=(1.5,7); z2=(4,7); z3=(1,0);
  z4=(4,0); z5=(11,6);
  draw z0{sklon2}..z1{right}..{-sklon1}z2{sklon1}..
       {sklon1}z3{-sklon1}..z4{right}..tension1.5..{sklon2}z5;
endchar;

beginchar(zcaron, 11u#, 12u#, 0);
  z0=(0,6); z1=(1.5,7); z2=(4,7); z3=(1,0);
  z4=(4,0); z5=(11,6);
  draw z0{sklon2}..z1{right}..{-sklon1}z2{sklon1}..
       {sklon1}z3{-sklon1}..z4{right}..tension1.5..{sklon2}z5;
  hacek shifted (2.5,0);
endchar;

beginchar("Z", 12u#, 14u#, 0);
  z1=(1,12);  z2=(4,14);  z3=(7.5,13);  z4=(9,14);  z4p=(x4+1,y4-.5);
  z5=(2,1);  z5d=(.5,0);  z5p=(-.5,.8);  z6=(6,0);  z7=(12,6);
  draw z1..z2{right}..z3..z4p{up}..z4{left}..z3..
       z5..z5d{left}..z5p{up}..z5..z6{right}..tension2..{sklon2}z7;
endchar;

beginchar(Zcaron, 12u#, 18u#, 0);
  z1=(1,12);  z2=(4,14);  z3=(7.5,13);  z4=(9,14);  z4p=(x4+1,y4-.5);
  z5=(2,1);  z5d=(.5,0);  z5p=(-.5,.8);  z6=(6,0);  z7=(12,6);
  draw z1..z2{right}..z3..z4p{up}..z4{left}..z3..
       z5..z5d{left}..z5p{up}..z5..z6{right}..tension2..{sklon2}z7;
  hacek shifted (6,6);
endchar;


%%%%%%%%%%%%%%%%%%%%%%% Digits %%%%%%%%%%%%%%%%%%%%%%%%%%%

beginchar("1", 8u#, 14u#, 0);
  z1=(1.5,9); z2=(5.5,14); z3=(2.5,0);
  draw z1..{dir60}z2--z3;
endchar;

beginchar("2", 8u#, 14u#, 0);
  z1=(1.5,11); z2=(4,14); z3=(6,11); z4=(0,0); z5=(5,0);
  draw z1..z2{right}..z3{-dir80}..{-dir65}z4--z5;
endchar;

beginchar("3", 8u#, 14u#, 0);
  z1=(1.5,11); z2=(4,14); z3=(6,11); z4=(3,8); z5=(2,0); z6=(-.5,3);
  draw z1..z2{right}..z3{-dir80}..z4&z4..tension1.5..z5{left}..z6{up};
endchar;

beginchar("4", 8u#, 14u#, 0);
  z1=(5,14); z2=(0,6); z3=(6,6); z4=(4.5,9); z5=(2.5,0);
  draw z1--z2--z3;
  draw z4--z5;
endchar;

beginchar("5", 8u#, 14u#, 0);
  z1=(6,14); z2=(2.5,14); z3=(1,8); z4=(4,9); z5=(2,0); z6=(-.5,3);
  draw z1--z2--z3&z3..z4..tension1.5..z5{left}..z6{up};
endchar;

beginchar("6", 8u#, 14u#, 0);
  z1=(6,14); z3=(.5,5); z4=(4,8); z5=(2,0); z6=(-.5,3);
  draw z1{-dir35}..z3{sklon1}&z3{-sklon1}..z4..tension1.5..z5{left}..
       z3{-sklon1};
endchar;

beginchar("7", 8u#, 14u#, 0);
  z1=(0.5,14); z2=(6,14); z3=(0,0); z4=(1.5,7); z5=(4.5,7);
  draw z1--z2--z3;
  draw z4--z5;
endchar;

beginchar("8", 8u#, 14u#, 0);
  z1=(4,14); z2=(3.2,8); z3=(2,0);
  draw z1{left}..z2..z3{left}..z2..{left}z1;
endchar;

beginchar("9", 8u#, 14u#, 0);
  z0=(2,13); z1=(6,14); z2=(1,7); z3=(2.5,0);
  z1p=(4,13.9);
  draw z1{left}..z1p..z0{-sklon2}..tension1.5..
       z2{right}..tension1.2..{-sklon1}z1{sklon1}..z3{sklon1};
endchar;

beginchar("0", 8u#, 14u#, 0);
  z1=(4,14); z3=(1,0);
  draw z1{left}..tension2.9..z3{right}..tension2.9..{left}z1;
endchar;


%%%%%%%%%%%%%%%%%%%%% Others %%%%%%%%%%%%%%%%%%%%%%%%%%%%%

beginchar("+", 8u#, 9u#, 0);
  z1=(1,7); z2=(6,7); z3=(3.3,4.5); z4=(3.7,9.5);
  draw z1--z2;
  draw z3--z4;
endchar;

beginchar(minus, 8u#, 7u#, 0);
  z1=(1,7); z2=(6,7);
  draw z1--z2;
endchar;

beginchar(times, 8u#, 9u#, 0);
  z1=(1.7,9); z2=(5.7,9); z3=(1.3,5); z4=(5.3,5);
  draw z1--z4;
  draw z2--z3;
endchar;

beginchar("*", 8u#, 9u#, 0);
  z1=(1.7,10); z2=(5.7,10); z3=(1.3,4); z4=(5.3,4);
  draw z1--z4;
  draw z2--z3;
  draw (0,7)--(7,7);
endchar;

beginchar("=", 8u#, 9u#, 0);
  z1=(1,6); z2=(6,6); z3=(1.2,8); z4=(6.2,8);
  draw z1--z2;
  draw z3--z4;
endchar;

beginchar(slash, 8u#, 14u#, 0);
  z1=(0,0); z2=(9,14);
  draw z1--z2;
endchar;

beginchar(backslash, 6u#, 14u#, 0);
  z1=(0,14); z2=(6,0);
  draw z1--z2;
endchar;

beginchar("<", 8u#, 14u#, 0);
  z1=(1,7); z2=(7,10); z3=(6,4);
  draw z3--z1--z2;
endchar;

beginchar(">", 8u#, 14u#, 0);
  z1=(7,7); z2=(2,10); z3=(1,4);
  draw z3--z1--z2;
endchar;

beginchar("@", 9u#, 9u#, 2u#);
  z0=(4,6); z1=(7,7); z2=(3,0); z3=(5.4,1);
  z1p=(5.5,6.9);
  z4=(6,0); z5=(9,4);  z6=(7,9);  z7=(1,0);  z8=(7,-1);
  draw z1{left}..z1p..z0{-sklon2}..tension1.5..
       z2{right}..tension1.2..{-sklon1}z1{sklon1}..z3{sklon1}..
       z4{right}..z5..z6..z7{dir-60}..z8;
endchar;

beginchar("(", 8u#, 16u#, 2u#);
  z1=(7,16); z2=(2,7); z3=(5,-2);
  draw z1..z2..z3;
endchar;

beginchar(")", 8u#, 16u#, 2u#);
  z1=(3,16); z2=(6,7); z3=(1,-2);
  draw z1..z2..z3;
endchar;

beginchar("[", 8u#, 14u#, 2u#);
  z1=(7,14); z1a=(4,14); z2a=(2,0); z2=(5,0);
  draw z1--z1a--z2a--z2;
endchar;

beginchar("]", 8u#, 14u#, 2u#);
  z1=(6,14); z1a=(3,14); z2a=(1,0); z2=(4,0);
  draw z1a--z1--z2--z2a;
endchar;

beginchar("{", 8u#, 14u#, 0);
  z1=(7,14); z7=(5.5,0);  z1a=(5,14);  z2a=(3,0);
  z2-z1a = whatever*(z2a-z1a);  y2=12;
  z3-z1a = whatever*(z2a-z1a);  y3=9;
  z4 = (2,7);
  z5-z1a = whatever*(z2a-z1a);  y5=5;
  z6-z1a = whatever*(z2a-z1a);  y6=2;
  draw z1{left}..z2..z3..z4&z4..z5..z6..{right}z7;
endchar;

beginchar("}", 8u#, 14u#, 0);
  z1=(2.5,14); z7=(1,0);  z1a=(5,14);  z2a=(3,0);
  z2-z1a = whatever*(z2a-z1a);  y2=12;
  z3-z1a = whatever*(z2a-z1a);  y3=9;
  z4 = (7,7);
  z5-z1a = whatever*(z2a-z1a);  y5=5;
  z6-z1a = whatever*(z2a-z1a);  y6=2;
  draw z1{right}..z2..z3..z4&z4..z5..z6..{left}z7;
endchar;

beginchar(percent, 10u#, 14u#, 0);
  z1=(0,0); z2=(9,14);  z3=(3,14);  z4=(2,9);
  draw z1--z2;
  draw z3..tension2..z4..tension2..cycle;
  draw (z3..tension2..z4..tension2..cycle) shifted (4,-y4);
endchar;

beginchar(promile, 13u#, 14u#, 0);
  z1=(0,0); z2=(9,14);  z3=(3,14);  z4=(2,9);
  draw z1--z2;
  draw z3..tension2..z4..tension2..cycle;
  draw (z3..tension2..z4..tension2..cycle) shifted (4,-y4);
  draw (z3..tension2..z4..tension2..cycle) shifted (8,-y4);
endchar;

beginchar("&", 7u#, 11u#, 0);
  z1=(5,0); z2=(3,7); z3=(4,11); z4=(0,1.5);  z5=(1,0);  z6=(7,7);
  draw z1..z2..z3{right}..z2..tension2..z4{sklon1}..z5{right}..tension2..
  {sklon2}z6;
endchar;

beginchar("$", 8u#, 15u#, 1u#);
  z1=(8,10.5); z2=(4.5,14); z2a=(1,10.5);
  z3=(4,7);
  z4=(3.5,0); z4a=(7,3.5);  z5=(0,3.5);
  draw z1..z2{left}..z2a..z3..z4a..z4{left}..z5;
  draw (2.5,-1)--(3.5,15);
  draw (4.5,-1)--(5.5,15);
endchar;

beginchar("#", 11.5u#, 14u#, 0);
  z1=(0,0); z2=(4,14);  z3=(10.5,0);
  draw (z1--z2) shifted (2,0);
  draw (z1--z2) shifted (6,0);
  draw (z1--z3) shifted (.5,5);
  draw (z1--z3) shifted (1.3,9);
endchar;

beginchar("|", 2u#, 14u#, 0);
  z1=(0,0); z2=(2,14);
  draw (z1--z2)
endchar;

beginchar("~", 8u#, 9u#, 0);
  z1=(0,6); z2=(3,8); z3=(5,6); z4=(8,8);
  draw z1..z2..z3..z4;
endchar;

beginchar(inch, 4u#, 15u#, 0);
  draw (1.8,15)--(1.5,12);
  draw (3.8,15)--(3.5,12);
endchar;

beginchar("^", 6u#, 14u#, 0);
  draw (2,12)--(4,14)--(6,12);
endchar;

beginchar("_", 8u#, 0, 7u#);
  draw (0,-7)--(8,-7);
endchar;


%%%%%%%%%%%%%%%%%%%%% Punctuation %%%%%%%%%%%%%%%%%%%%%%%%

beginchar("-", 3u#, 7u#, 0);
  draw (0,4)--(3,4);
endchar;

beginchar(dash, 12u#, 7u#, 0);
  z1=(1,7); z2=(10,7);
  draw z1--z2;
endchar;

beginchar("!", 6u#, 14u#, 0);
  pickup pencircle scaled (dotkoef*thin);
  drawdot (2,0);
  pickup pencircle scaled thin;
  draw (2.6, 4)..(5,14);
endchar;

beginchar("?", 8u#, 14u#, 0);
  pickup pencircle scaled (dotkoef*thin);
  drawdot (4,0);
  pickup pencircle scaled thin;
  z1=(3,12);  z2=(6,14);  z3=(5,8.5);  z4=(4.5,3);  z5=(7,5);
  draw z1..z2{right}..z3..z4{right}..z5;
endchar;

beginchar(",", 3u#, 1u#, 0);
  draw (0,1)--(-1,-2);
endchar;

beginchar(".", 3u#, 1u#, 0);
  pickup pencircle scaled (dotkoef*thin);
  drawdot (0,0);
  pickup pencircle scaled thin;
endchar;

beginchar(":", 4u#, 7u#, 0);
  pickup pencircle scaled (dotkoef*thin);
  drawdot (1,0);  drawdot (2,7);
  pickup pencircle scaled thin;
endchar;

beginchar(";", 4u#, 7u#, 0);
  pickup pencircle scaled (dotkoef*thin);
  drawdot (2,7);
  pickup pencircle scaled thin;
  draw (1,1)--(0,-2);
endchar;

beginchar(clqq, 4u#, 1u#, 0);
  draw (0,1)--(-1,-2);
  draw (2,1)--(1,-2);
endchar;

beginchar(crqq, 4u#, 15u#, 0);
  draw (2,15)--(1.5,12);
  draw (4,15)--(3.5,12);
endchar;

beginchar(leftquota, 4u#, 15u#, 0);
  draw (0,15){down}..(1,12);
endchar;

beginchar(rightquota, 4u#, 15u#, 0);
  draw (4,15){down}..(2,12);
endchar;



%%%%%%%%%%%%%%%%%%%%% special parts of characters %%%%%%%%%%%%%%%%%

beginchar(1, .7u#, 7u#, 0);  %% pravy konec znaku
  draw (0,6)..(.7,7);
endchar;

beginchar(2, 5u#, 7u#, 0);  %% levy rovny zacatek znaku
  draw (0,0)..(5,6);
endchar;

beginchar(3, 6u#, 7u#, 0);  %% levy prohnuty zacatek znaku
  draw (0,0){(3,2)}..{sklon2}(6,6);
endchar;

beginchar(4, 5u#, 7u#, 0);  %% obecna konvexni spojka za verzalkou a s
  draw (-4,0){right}..{sklon2}(5,6);
endchar;

beginchar(5, 7u#, 7u#, 0);  %% delsi konvexni spojka za verzalkou P
  draw (-4,0){right}..{sklon2}(7,6);
endchar;

beginchar(6, 3u#, 7u#, 0);  %% kratsi konvexni spojka pro dvojice sv sn
  draw (-4,0){right}..{sklon2}(3,6);
endchar;

beginchar(7, 8u#, 7u#, 0);  %% nabehova carka pro male x
  draw (0,2){down}..(2,0){right}..tension2..{sklon2}(8,6);
endchar;


%%%%%%%%%%%%%%%%%%%%%% Ligtables %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

boundarychar:=1;
leftboundary:=1;

ligtable ",":  "," =: clqq;
ligtable leftquota:  leftquota =: crqq, skipto leftboundary;
ligtable "-":  "-" =: minus, skipto leftboundary;
ligtable minus: "-" =: dash, skipto leftboundary;

def rightboundaries =
      1 |=:> 1,
      "-" |=:|>> 1,
      "!" |=:|>> 1,
      "?" |=:|>> 1,
      "," |=:|>> 1,
      ":" |=:|>> 1,
      ";" |=:|>> 1,
      crqq |=:|>> 1,
      rightquota |=:|>> 1,
      slash |=:|>> 1,
      "&" |=:|>> 1,
      "+" |=:|>> 1,
      "]" |=:|>> 1,
      ")" |=:|>> 1,
      "." |=:|>> 1
enddef;

ligtable  ||: clqq: dash: slash: "(": "[": "&": "+": leftboundary::
      "a" kern kk#,  aacute kern kk#,
      "b" |=:|> 3,
      "c" kern kk#, ccaron kern kk#,
      "d" kern kk#, dcaron kern kk#,
      "e" |=:|> 3, eacute |=:|> 3, ecaron |=:|> 3,
      "f" |=:|> 3,
      "g" kern kk#,
      "h" |=:|> 3,
      "i" |=:|> 3, iacute |=:|> 3,
      "j" |=:|> 3,
      "k" |=:|> 3,
      "l" |=:|> 3,
      "m" |=:|> 2,
      "n" |=:|> 2,  ncaron |=:|> 2,
      "o" kern kk#,  oacute kern kk#,
      "p" |=:|> 3,  "q" kern kk#,
      "r" |=:|> 3,  rcaron |=:|> 3,
      "s" |=:> sleft,  scaron |=:> scaronleft,
      "u" |=:|> 3,  uring |=:|> 3, uacute |=:|> 3,
      "t" |=:|> 3,  tcaron |=:|> 3,
      "v" |=:|> 2,  "w" |=:|> 2,
      "x" |=:|> 7,
      "y" |=:|> 2,  yacute |=:|> 2,
      "z" |=:|> 2,  zcaron |=:|> 2;

ligtable "B": "D": Dcaron: "F": "I": Iacute: "O": Oacute:
   "S": Scaron:
   "T": Tcaron: "V": "W":
      "a" |=:| 4,  aacute |=:| 4,
      "b" |=:| 4,
      "c" |=:| 4,  ccaron |=:| 4,
      "d" |=:| 4,  dcaron |=:| 4,
      "e" |=:| 4, eacute |=:| 4, ecaron |=:| 4,
      "f" |=:| 4,
      "g" |=:| 4,
      "h" |=:| 4,
      "i" |=:| 4, iacute |=:| 4,
      "j" |=:| 4,
      "k" |=:| 4,
      "l" |=:| 4,
      "m" |=:| 4,
      "n" |=:| 4,  ncaron |=:| 4,
      "o" |=:| 4,  oacute |=:| 4,
      "p" |=:| 4,  "q" |=:| 4,
      "r" |=:| 4,  rcaron |=:| 4,
      "s" |=:| 4,  scaron |=:| 4,
      "u" |=:| 4,  uring |=:| 4, uacute |=:| 4,
      "t" |=:| 4,  tcaron |=:| 4,
      "v" |=:| 4,  "w" |=:|> 4,
      "x" |=:| 4,
      "y" |=:| 4,  yacute |=:| 4,
      "z" |=:| 4,  zcaron |=:| 4;

ligtable "P":
      "a" |=:| 5,  aacute |=:| 5,
      "b" |=:| 5,
      "c" |=:| 5,  ccaron |=:| 5,
      "d" |=:| 5,  dcaron |=:| 5,
      "e" |=:| 5, eacute |=:| 5, ecaron |=:| 5,
      "f" |=:| 5,
      "g" |=:| 5,
      "h" |=:| 5,
      "i" |=:| 5, iacute |=:| 5,
      "j" |=:| 5,
      "k" |=:| 5,
      "l" |=:| 5,
      "m" |=:| 5,
      "n" |=:| 5,  ncaron |=:| 5,
      "o" |=:| 5,  oacute |=:| 5,
      "p" |=:| 5,  "q" |=:| 5,
      "r" |=:| 5,  rcaron |=:| 5,
      "s" |=:| 5,  scaron |=:| 5,
      "u" |=:| 5,  uring |=:| 5, uacute |=:| 5,
      "t" |=:| 5,  tcaron |=:| 5,
      "v" |=:| 5,  "w" |=:|> 5,
      "x" |=:| 5,
      "y" |=:| 5,  yacute |=:| 5,
      "z" |=:| 5,  zcaron |=:| 5;

ligtable
   scarondepth:  scaronleft:  scaron:
      "!" kern 1u#, "?" kern 2u#,
   "s":  sleft:  sdepth:
      "a" |=:| 4,  aacute |=:| 4,
      "b" |=:| 4,
      "c" |=:| 4,  ccaron |=:| 4,
      "d" |=:| 4,  dcaron |=:| 4,
      "e" |=:| 4, eacute |=:| 4, ecaron |=:| 4,
      "f" |=:| 4,
      "g" |=:| 4,
      "h" |=:| 4,
      "i" |=:| 4, iacute |=:| 4,
      "j" |=:| 4,
      "k" |=:| 4,
      "l" |=:| 4,
      "m" |=:| 6,
      "n" |=:| 6,  ncaron |=:| 6,
      "o" |=:| 4,  oacute |=:| 4,
      "p" |=:| 4,  "q" |=:| 4,
      "r" |=:| 4,  rcaron |=:| 4,
      "s" |=:| 4,  scaron |=:| 4,
      "u" |=:| 4,  uring |=:| 4, uacute |=:| 4,
      "t" |=:| 6,  tcaron |=:| 6,
      "v" |=:| 6,  "w" |=:|> 6,
      "x" |=:| 4,
      "y" |=:| 6,  yacute |=:| 6,
      "z" |=:| 6,  zcaron |=:| 6;


ligtable "b":
     "m" =:| bnarrow,
     "n" =:| bnarrow,  ncaron =:| bnarrow,
     "v" =:| bnarrow,  "w" =:| bnarrow,
     "y" =:| bnarrow,  yacute =:| bnarrow,
     rightboundaries;
ligtable "o":
     "m" =:| onarrow,
     "n" =:| onarrow,  ncaron =:| onarrow,
     "v" =:| onarrow,  "w" =:| onarrow,
     "y" =:| onarrow,  yacute =:| onarrow,
     rightboundaries;
ligtable oacute:
     "m" =:| oacutenarrow,
     "n" =:| oacutenarrow,  ncaron =:| oacutenarrow,
     "v" =:| oacutenarrow,  "w" =:| oacutenarrow,
     "y" =:| oacutenarrow,  yacute =:| oacutenarrow,
     rightboundaries;
ligtable "v":
     "m" =:| vnarrow,
     "n" =:| vnarrow,  ncaron =:| vnarrow,
     "v" =:| vnarrow,  "w" =:| vnarrow,
     "y" =:| vnarrow,  yacute =:| vnarrow,
     rightboundaries;
ligtable "w":
     "m" =:| wnarrow,
     "n" =:| wnarrow,  ncaron =:| wnarrow,
     "v" =:| wnarrow,  "w" =:| wnarrow,
     "y" =:| wnarrow,  yacute =:| wnarrow,
     rightboundaries;

ligtable
   "g": "G":
   "j": "J":
   "q": "Q":
   "y": yacute: "Y": Yacute:
        "s" |=: sdepth,  scaron |=: scarondepth,
        rightboundaries;

ligtable "a": aacute: "A": Aacute:
   "c": ccaron: "C": Ccaron:
   "d": dcaron:
   "e": eacute: ecaron: "E": Eacute: Ecaron:
   "f":
   "h": "H":
   "i": iacute:
   "k": "K":
   "l": "L":
   "m": "M":
   "n": ncaron: "N": Ncaron:
   "p":
   "r": rcaron: "R": Rcaron:
   "t": tcaron:
   "u": uring: uacute: "U": Uacute: Uring:
   "x": "X":
   "z": zcaron: "Z": Zcaron:
        rightboundaries;

fontdimen 1: .3pt#, 3u#, 2.5u#, 1u#, 7u#, 20u#, 2.5u#;
designsize := 25u#;

end;

